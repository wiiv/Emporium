buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.10.2-" + getVersion("VERSION")
group = "wiiv"
archivesBaseName = "emporium"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
	version = "1.10.2-12.18.3.2254"
	runDir = "run"
	
	mappings = "stable_26"
	useDepAts = false
}

repositories {
	mavenCentral()
	maven {
		name = "progwml6"
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = "ChickenBones"
		url "http://chickenbones.net/maven"
	}
}

dependencies {
	deobfCompile "mezz.jei:jei_1.10.2:+"
	compile "codechicken:CodeChickenLib:1.10.2-2.5.2.212:deobf"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
		
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

String getVersion(String type) {
	String major = "0";
	String revision = "0";
	String patch = "0";
	String prefix = "public static final String $type = \"";
	File file = file("src/main/java/wiiv/emporium/Globals.java")
	file.eachLine { String s ->
		s = s.trim();
		if (s.startsWith(prefix)) {
			s = s.substring(prefix.length(), s.length() - 2);
			String[] pts = s.split("\\.");

			major = pts[0];
			revision = pts[1];
			patch = pts[2];
		}
	}
	return "$major.$revision.$patch";
}
